# Copyright 2012-2013, Qualcomm Innovation Center, Inc.
# 
#    Licensed under the Apache License, Version 2.0 (the "License");
#    you may not use this file except in compliance with the License.
#    You may obtain a copy of the License at
# 
#        http://www.apache.org/licenses/LICENSE-2.0
# 
#    Unless required by applicable law or agreed to in writing, software
#    distributed under the License is distributed on an "AS IS" BASIS,
#    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#    See the License for the specific language governing permissions and
#    limitations under the License.
#
import os

Import('env')

vars = Variables();

vars.Add(PathVariable('XUNIT_DIR', 'The path to xUnit.net unit testing framework',  os.environ.get('XUNIT_DIR'), PathVariable.PathIsDir))

vars.Update(env)

Help(vars.GenerateHelpText(env))

# AllJoyn Unity Unit Test Sources
envtest = env.Clone()
srcs = envtest.Glob('*.cs')

if envtest['OS_GROUP'] == 'windows':
    envtest['CSC'] = 'csc.exe'
    if envtest['VARIANT'] == 'debug':
        envtest['CSC_FLAGS']=['debug']
elif envtest['OS_GROUP'] == 'posix':
    envtest['CSC'] = 'gmcs'
    envtest['CSC_FLAGS']=['debug']

if(env.has_key('XUNIT_DIR')):
    print("Building Unit Test for Unity Binding")
    envtest['CSC_LIB_PATHS'] = [envtest['XUNIT_DIR'], Dir(env['UNITY_DISTDIR'] + os.sep + 'lib').abspath]
    envtest['CSC_REFERENCES'] = ['xunit.dll', 'alljoyn_unity.dll']
    alljoyn_unity_test = envtest.CSharpLib(source = srcs , target='$UNITY_TESTDIR/AllJoynUnityTest.dll')
    envtest.Depends(alljoyn_unity_test, env['ALLJOYN_UNITY_LIB']);
    
    # In addition to AllJoynUnitTest.dll the unit tests require xunit.dll, 
    # alljoyn_unity.dll, and alljoyn_c.dll to run
    # place alljoyn_unity.dll into the test dir
    envtest.Install('$UNITY_TESTDIR', envtest['ALLJOYN_UNITY_LIB'])
    #place alljoyn_c.dll into the test dir
    envtest.Install('$UNITY_TESTDIR', envtest['ALLJOYN_C_LIB_SHARED'])
    #place alljoyn_c.dll into the unit_test/bin/$VARIANT directory to facilitate development
    if envtest['VARIANT'] == 'debug':
        envtest.Install('bin/Debug', envtest['ALLJOYN_C_LIB_SHARED'])
    elif envtest['VARIANT'] == 'release':
        envtest.Install('bin/Release', envtest['ALLJOYN_C_LIB_SHARED'])
    # place xunit.dll into the test dir
    envtest.Install('$UNITY_TESTDIR', envtest['XUNIT_DIR'] + os.sep + 'xunit.dll')
    # place xunit.dll into project folder so unit test can be built and run 
    # from visual studio very useful for development and debugging.
    envtest.Install('..' + os.sep, envtest['XUNIT_DIR'] + os.sep + 'xunit.dll')
    
    Return('alljoyn_unity_test')